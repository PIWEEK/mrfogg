buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.jfrog.org/artifactory/gradle-plugins'
        }
        maven {
            name 'Gradle Shadow'
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:1.1.0"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.0.16'
        classpath 'org.gradle.plugins:shadow:0.7.4'
    }
}

apply plugin: 'cobertura'
apply plugin: 'codenarc'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'shadow'

version=currentVersion
group=currentGroup

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

// Run code narc before the test phase begins
test.dependsOn codenarcMain

repositories {
    mavenCentral()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
    groovy "org.codehaus.groovy:groovy-all:${groovyVersion}"

    compile "org.springsource.springloaded:springloaded-core:1.1.4"

    compile "com.yammer.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-testing:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-hibernate:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-migrations:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-auth:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-client:${dropwizardVersion}"
    compile "com.yammer.dropwizard:dropwizard-views:${dropwizardVersion}"

    compile 'com.h2database:h2:1.3.170'

    testCompile "com.yammer.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.0-SNAPSHOT"
    testRuntime "cglib:cglib-nodep:2.2.2"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:1.2"    // allows mocking of classes without default constructor (together with CGLIB)

    // needed for validation tests so logging in hibernate validator does not throw exception
    compile 'ch.qos.logback:logback-classic:1.0.7'
    compile "org.slf4j:log4j-over-slf4j:1.7.2"

    compile project(":mrfogg-common")
    compile project(":mrfogg-widget-image")
}

jar {
    manifest {
        attributes 'Main-Class': "${mainClass}"
        attributes 'Built-Date': new Date() //now
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor-Id': project.group
        attributes 'Premain-Class': 'org.springsource.loaded.agent.SpringLoadedAgent'
        attributes 'Can-Redefine-Classes': true
    }
}

cobertura {
    coverageFormats = ['xml', 'html']
    coverageIncludes = ['**/*.java', '**/*.groovy']
    coverageSourceDirs = [sourceSets.main.allSource.srcDirs]
}

codenarc {
    configFile = rootProject.file('src/test/resources/codenarcrule.groovy')
    reportFormat = System.properties['codeNarcOutput'] ?: 'html'
}

shadow {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

task run(dependsOn: 'shadow', group: 'dropwizard',  description: "Run service using dev configuration") << {
    // Grab any dw. properties that are specified on the command line and pass to the Java process (i.e. dw.http.port=8080)
    def props = project.ext.properties.findAll { it.key.startsWith('dw') }

    // Add spring loaded agent
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'server', 'dev_config.yml']
        systemProperties = props
    }
}

task migrate(dependsOn: 'shadow', group: 'dropwizard', description: "Run migrations using the dev configuration") << {
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'db', 'migrate', 'dev_config.yml']
    }
}

task dropAll(dependsOn: 'shadow', group: 'dropwizard',  description: "Run drop-all database command") << {
    javaexec {
        main = '-jar'
        args = ["${shadow.shadowJar.getPath()}", 'db', 'drop-all', 'dev_config.yml', '--confirm-delete-everything']
    }
}

/* This will re-build the project if there is any change in the code */
task watch(type: net.kaleidos.gradle.watch.ContinuousTask) { }

/* Running the project from the code not from the fat-jar */
task devRun(type: JavaExec, group: 'dropwizard', description:'Run service with reloading enabled') {
    main = mainClass
    classpath = classpath
    args = ['server', 'dev_config.yml']
    jvmArgs = ["-javaagent:${shadow.shadowJar.getPath()}", "-noverify"]
}
